##############################################################################
#
#   manage/packages/deep_learning.cf - EECS deep learning package installation
#   Maintained: Ben Roose, ver 0.1
#
###############################################################################



# PROTIP: Use body file control to help make modular policy.
# "There can be only one" body common control per policy entry (promises.cf,update.cf)

# TESTING ONLY:INPUT STANDARD LIBRARY and HOST CLASSIFICATION (Remove after testing)
body file control
{
      inputs => { "$(sys.libdir)/stdlib.cf",
		  "$(sys.workdir)/inputs/services/eecs/classification/host_role/main.cf" };
}

######################################################################
# Public entry point: install_deep_learning_packages
######################################################################

# NOTES for future development of configuration:
# 1.

bundle agent install_deep_learning_packages
# @description: install all specialist packages for deep learning research, used by Dr. Sinha
{
  vars:
      "pip_package_dir" string => "/usr/local/lib/python2.7/dist-packages/";
      
    debian.!ubuntu::
      # dependency repository packages for pure debian
      "apt_packages" slist => {
				"build-essential",
				"python",
				"python-sss",
				"python-dev",
				"python-pip",
				"python-numpy",
				"python-scipy",
				"python-sklearn",
				"python-nose",
				"python-yaml",
				"python-docopt",
				"python-matplotlib",
				"python-biopython",
				"python-virtualenv",
				"python-pip",
      };

    ubuntu::
      # dependency repository packages for ubuntu
      "apt_packages" slist => {
				"build-essential",
				"python",
				"python-dev",
				"python-pip",
				"python-numpy",
				"python-scipy",
				"python-sklearn",
				"python-nose",
				"python-yaml",
				"python-docopt",
				"python-matplotlib",
				"python-biopython",
				"python-virtualenv",
				"python-pip",
				"ubuntu-make",
				"sublime-text",
      };

      # deep learning software required - reverse order for installation?
    ubuntu_14::
      "pip_packages" slist => {
				"keras",
				"Theano",
      };

    ubuntu_16.deep_learning::
      "pip_packages" slist => {
				"keras",
				"tensorflow",
				"Theano",
      };
    ubuntu_16.deep_learning_gpu::
      "pip_packages" slist => {
				"keras",
				"tensorflow-gpu",
				"Theano",
      };

      # once ubuntu-make is installed pycharm and android-studio should now be able to be installed by user with:
      # $ umake ide pycharm

  files:
    ubuntu::
      "$(pip_package_dir)"
        perms => m( "a+rx" ),
        depth_search => recurse_with_base("inf"),
        comment => "sets permissions recursively on installed python package for all users to use package";

  methods:
    ubuntu::
      "install dependency package list" usebundle => package_latest("$(apt_packages)"),
      classes => results("bundle", "dependent_packages");

      # "install ubuntu-make version 17.03 or above"
      # 	usebundle => package_specific_latest( "ubuntu-make","17.03",$(sys.arch) ),
      #   classes => results("bundle", "umake_install");

  packages:
    ubuntu.dependent_packages_kept::
      "$(pip_packages)" package_method => pip("--no-cache-dir"), package_policy => "add",
      classes => results("bundle", "pip_packages"),
      comment => "install pip package list after dependent packages are installed without dependencies";      
      
  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";

    (DEBUG|DEBUG_deep_learning)::
      "-== $(this.bundle) ==-";
      "Repository packages to install: $(apt_packages)";
      "Python-pip packages to install: $(pip_packages)";
    (DEBUG|DEBUG_deep_learning).dependent_packages_repaired::
      "Repository packages were installed";
    (DEBUG|DEBUG_deep_learning).ubuntu_make_install_repaired::
      "ubuntu-make package was installed";
    (DEBUG|DEBUG_deep_learning).pycharm_install_repaired::
      "pycharm package was installed";
    (DEBUG|DEBUG_deep_learning).!ubuntu::
      "Deep learning packages cannot be installed on this operating system by CFEngine";
}

######################################################################
