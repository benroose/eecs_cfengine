##############################################################################
#
#   manage/packages/main.cf - EECS packages management
#   Maintained: Ben Roose, ver 0.3
#
###############################################################################



# PROTIP: Use body file control to help make modular policy.
# "There can be only one" body common control per policy entry (promises.cf,update.cf)

# TESTING ONLY:INPUT STANDARD LIBRARY (Remove after testing)
#body file control
#{
#      inputs => { "$(sys.libdir)/stdlib.cf" };
#}

######################################################################
# Public entry point: manage_packages bundle
######################################################################

# NOTES for future development of configuration:
# 1. move the installs/removals lists into an external json file
# 2. enable a method to specify one-off package installation within the classification hosts roles json file
# 3. figure out how to upgrade the Linux kernel and security packages from the package management end

bundle agent manage_packages
# @description: package management for hosts classified by: core (all hosts), workstation only packages, server only packages. Allows for software installation (package_latest) and removal (package_absent)
{

  vars:
     "core_installs" slist => {},
      policy => "overridable";
     "workstation_installs" slist => {},
      policy => "overridable";
     "server_installs" slist => {},
      policy => "overridable";
     "core_removals" slist => {},
      policy => "overridable";
     "workstation_removals" slist => {},
      policy => "overridable";
     "server_removals" slist => {},
      policy => "overridable";

    ## Required packages to be INSTALLED
    debian|ubuntu::
      "core_installs" slist => {
				  "iptables",
				  "openssh-client",
				  "emacs",
				  "screen",
				  "build-essential",
                                };
      
    workstation.(debian|ubuntu):: 
      # Unattended upgrades are dangerous, so only use automated security upgrades for single user workstations
      "workstation_installs" slist => {
					"unattended-upgrades",
					"update-notifier-common",
					"libreoffice",
					"chromium-browser",
					"firefox",
                                 };
      
    server.(debian|ubuntu)::
      "server_installs" slist => {
				   "openssh-server",
                                 };

    ## Required packages to be REMOVED
    debian|ubuntu::
      "core_removals" slist => {
                               };
      
    workstation.(debian|ubuntu)::      
      "workstation_removals" slist => {
					"openssh-server",
                                 };
      
    server.(debian|ubuntu)::
      "server_removals" slist => {
		                 };
    any::
      "all_installs"       slist => {@(core_installs), @(workstation_installs), @(server_installs)};
      "all_removals"       slist => {@(core_removals), @(workstation_removals), @(server_removals)};

  methods:
      "install_update_listed packages" usebundle => package_latest("$(all_installs)");
      "remove_listed packages" usebundle => package_absent("$(all_removals)");

    workstation.(debian|ubuntu)::
      "add unattended-upgrades config files" usebundle => unattended_upgrades_config;

      
    #SETTING HOST MANUALLY FOR MATLAB INSTALL (need to build host classification bundles to make this more generic)
#    jb_256_3432_res1::
#      "install_matlab" usebundle => install_matlab;   
  
  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";
      
    (DEBUG|DEBUG_manage_packages)::
      "Package to be installed: $(all_installs)";
      "Package to be removed: $(all_removals)";
}

###################################

bundle agent unattended_upgrades_config
# @description: creates and edits configuration files for enabling the debian/ubuntu unattended-upgrades package 
{
  vars:
      "enable_unattended_file" string => "/etc/apt/apt.conf.d/20auto-upgrades";

      "added_lines" slist => {
			 "APT::Periodic::Update-Package-Lists \"1\";",
			 "APT::Periodic::Unattended-Upgrade \"1\";",
                };

  files:
      "$(enable_unattended_file)"
      create => "true",
      perms => mog("644","root","root"),
      edit_line => append_if_no_line("$(added_lines)"),
      comment => "creates config file for enabling the unattended-upgrades package. Can also be enabled manually by running on system: # dpkg-reconfigure unattended-upgrades";

  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";
      
    (DEBUG|DEBUG_unattended_upgrades_config)::
      "file to add for enabling unattended upgrades: $(enable_unattended_file)";
      "Lines to be added into file: $(added_lines)";
}
