##############################################################################
#
#   manage/packages/installation.cf - EECS package installation management
#   Maintained: Ben Roose, ver 0.5
#
###############################################################################



# PROTIP: Use body file control to help make modular policy.
# "There can be only one" body common control per policy entry (promises.cf,update.cf)

# TESTING ONLY:INPUT STANDARD LIBRARY (Remove after testing)
body file control
{
      inputs => { "$(sys.libdir)/stdlib.cf" };
}

######################################################################
# Public entry point: multiple bundle
######################################################################

# NOTES for future development of configuration:
# 1. move the installs/removals lists into an external json file
# 2. enable a method to specify one-off package installation within the classification hosts roles json file
# 3. figure out how to upgrade the Linux kernel and security packages from the package management end

bundle agent manage_repository_packages
# @description: package management using standard repositories (i.e. apt) for hosts classified by: core (all hosts), workstation only packages, server only packages. Allows for software installation (package_latest) and removal (package_absent)
{

  vars:
     "extra_installs" slist => {},
      policy => "overridable";
     "core_installs" slist => {},
      policy => "overridable";
     "workstation_installs" slist => {},
      policy => "overridable";
     "server_installs" slist => {},
      policy => "overridable";
     "core_removals" slist => {},
      policy => "overridable";
     "workstation_removals" slist => {},
      policy => "overridable";
     "server_removals" slist => {},
      policy => "overridable";

    ## Required packages to be INSTALLED
    install_extra_packages::
      "extra_installs" slist => getvalues( "classify_host.extra_packages" );
      "packages_json_str" string => format("%S", "classify_host.extra_packages");

    debian|ubuntu::
      "core_installs" slist => {
 				  "unattended-upgrades",
				  "iptables",
				  "openssh-client",
				  "emacs",
				  "screen",
				  "tmux",
				  "build-essential",
                                };
      
    workstation.(debian|ubuntu):: 
      # libvte9 is needed by geany to show a terminal window inside
      "workstation_installs" slist => {
					"network-manager-openconnect-gnome",
					"arandr",
					"libreoffice",
					"chromium",
					"firefox-esr",
					"filezilla",
					"terminator",
					"default-jre",
					"default-jdk",
					"eclipse",
					"eclipse-cdt",
					"geany",
					"geany-plugins",
					"libvte9",
					"texmaker",
					"git",
                                 };
      
    server.!teaching.(debian|ubuntu)::
      "server_installs" slist => {
				   "openssh-server",
				   "molly-guard",
                                 };

    server.teaching.(debian|ubuntu)::
      "server_installs" slist => {
				   "openssh-server",
				   "molly-guard",
				   "tree",
				   "terminator",
				   "default-jre",
				   "default-jdk",
				   "eclipse",
				   "eclipse-cdt",
				   "geany",
				   "geany-plugins",
				   "subversion",
				   "git",
				   "git-el",
                                 };

    ## Required packages to be REMOVED
    debian|ubuntu::
      "core_removals" slist => {
                               };
      
    workstation.(debian|ubuntu)::      
      "workstation_removals" slist => {
					"openssh-server",
					"openssh-sftp-server",
                                 };
      
    server.(debian|ubuntu)::
      "server_removals" slist => {
		                 };
    any::
      "all_installs"       slist => {@(core_installs), @(workstation_installs), @(server_installs), @(extra_installs)};
      "all_removals"       slist => {@(core_removals), @(workstation_removals), @(server_removals)};

  methods:
      "install_update_listed packages" usebundle => package_latest("$(all_installs)");
      "remove_listed packages" usebundle => package_absent("$(all_removals)");

    workstation.(debian|ubuntu)::
      "add unattended-upgrades config files" usebundle => unattended_upgrades_config;
      
  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";
      
    (DEBUG|DEBUG_manage_packages)::
      "=== $(this.bundle) ===";
      "Repository package to be installed: $(all_installs)";
      "Repository package to be removed: $(all_removals)";
      
    (DEBUG|DEBUG_manage_packages).install_extra_packages::
      "Extra repository packages specifically for this host: $(packages_json_str)";
}

###################################

bundle agent manage_custom_packages
# @description: custom scripting for packages required by users but not in standard repositories
{
  vars:

      "texworks_dictionary_source" string => "/usr/share/hunspell";
      "texworks_dictionary_target" string => "/usr/share/myspell/dicts";

  classes:      

    "autostart_scmmanager"
	expression => "scmmanager",
        scope => "namespace",
        comment => "create global class for autostart if scmmannager is listed in custom_installs";
      
  packages:
    pip_gensim::
      "gensim" package_method => pip(""), package_policy => "add",
      comment => "install the gensim package using python pip (used by Sara Bahrami)";

    pip_theano::
      "theano" package_method => pip(""), package_policy => "add",
      comment => "install the theano package using python pip (used by Dr. Kaushik Sinha)";

    pip_pattern::
      "pattern" package_method => pip(""), package_policy => "add",
      comment => "install the pattern package using python pip (used by Dr. Kaushik Sinha)";

    pip_orange::
      "orange" package_method => pip(""), package_policy => "add",
      comment => "install the orange package using python pip (used by Dr. Kaushik Sinha)";

  files:
    texworks_dictionaries::
      "$(texworks_dictionary_target)"
      move_obstructions => "true",
      link_from => linkchildren( "$(texworks_dictionary_source)" ),
      comment => "create symbolic links for all libreoffice dictionaries in texworks dictionary directory";

  methods:

    ad_authentication::
      "configure sssd or kerberos and join host to ad domain" usebundle => join_host_to_ad;
      
    ldap_removal::
      "remove old ldap config files and packages" usebundle => remove_ldap;

    uaf2_client::
      "install and configure tom's uaf2_client" usebundle => install_uaf2_client;

    linux_show_player::
      "install linux-show-player" usebundle => install_linux_show_player;

    etc_offline_editor::
      "install etc offline editor script" usebundle => install_etc_offline_editor;
      
    skype::
      "install skype" usebundle => install_skype;

    pyrosetta::
      "install python pyrosetta" usebundle => install_pyrosetta;

    python_graphics_lib::
      "install python graphics library (for Keenan Jackson)" usebundle => install_python_graphics;

    cuda_by_example::
      "install cuda_by_example code and nvidia cuda toolkit" usebundle => install_cuda_by_example;

    deep_learning|deep_learning_gpu::
      "install all packages for deep learning (used by Dr Sinha)" usebundle => install_deep_learning_packages,
      inherit => "true";

      
#   matlab_research|matlab_classroom::
#      "install matlab software" usebundle => install_matlab,
#      inherit => "true",
#      comment => "run the set of custom bundles for installing the commercial matlab software. Must specify whether to use the research or classroom license";
      
  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";
      
    (DEBUG|DEBUG_manage_packages)::
      "=== $(this.bundle) ===";
      "Custom packages to be installed: $(classify_host.custom_packages)";

    (DEBUG|DEBUG_manage_packages).test_package::
      "test_package is listed in host_data.json file for this host and a test_package class has been created";      
}

######################################################################

bundle agent unattended_upgrades_config
# @description: creates and edits configuration files for enabling the debian/ubuntu unattended-upgrades package 
{
  vars:
      "enable_unattended_file" string => "/etc/apt/apt.conf.d/20auto-upgrades";

      "added_lines" slist => {
			 "APT::Periodic::Update-Package-Lists \"1\";",
			 "APT::Periodic::Unattended-Upgrade \"1\";",
                };

  files:
      "$(enable_unattended_file)"
      create => "true",
      perms => mog("644","root","root"),
      edit_line => append_if_no_line("$(added_lines)"),
      comment => "creates config file for enabling the unattended-upgrades package. Can also be enabled manually by running on system: # dpkg-reconfigure unattended-upgrades";

  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";
      
    (DEBUG|DEBUG_manage_packages)::
      "=== $(this.bundle) ===";
      "file to add for enabling unattended upgrades: $(enable_unattended_file)";
      "Lines to be added into file: $(added_lines)";
}

######################################################################
